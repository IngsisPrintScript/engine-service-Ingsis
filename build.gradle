plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'checkstyle'
	id 'com.diffplug.spotless' version '6.25.0'
    id 'maven-publish'
}

group = 'com.ingsis'
version = '0.0.1-SNAPSHOT'
description = 'Snippet Service for Ingsis'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/austral-ingsis/*")
        credentials {
            username = findProperty("gpr.user")
            password = findProperty("gpr.key")
        }
    }
    maven {
        name = "PrintScriptPackages"
        url = uri("https://maven.pkg.github.com/IngsisPrintScript/PrintScript")
        credentials {
            username = findProperty("gpr.user")
            password = findProperty("gpr.key")
        }
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	implementation 'PrintScript:com.ingsis.engine:1.0.0-ad80526'

	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.azure:azure-storage-blob:12.27.0'
	implementation 'com.azure:azure-identity:1.12.2'
	implementation 'org.austral.ingsis:redis-streams-mvc:0.1.13'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    //### PRINTSCRIPT ###//
    implementation ("PrintScript:com.ingsis.engine:0.0.0-ci.84.3de9b5c")
    implementation ("PrintScript:com.ingsis.utils:0.0.0-ci.84.3de9b5c")
}

tasks.register("installGitHooks") {
    doLast {
        if (System.getProperty("os.name").toLowerCase().contains("win")) {
            exec {
                commandLine "bash", "./scripts/install-hooks.sh"
            }
        } else {
            exec {
                commandLine "sh", "./scripts/install-hooks.sh"
            }
        }
    }
}
tasks.named("build") {
    dependsOn("installGitHooks")
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy tasks.jacocoTestReport
}

jacoco {
	toolVersion = "0.8.10"
}

jacocoTestReport {
	dependsOn tasks.test
	reports {
		xml.required = true
		html.required = true
	}
}

spotless {
    java {
        target 'src/**/*.java'
        eclipse().configFile("config/eclipse-java-style.xml")
        importOrder()
        removeUnusedImports()
        lineEndings = 'UNIX'
    }
}
checkstyle {
    toolVersion = '10.12.0'
    configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
}